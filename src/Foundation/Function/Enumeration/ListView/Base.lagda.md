---
url: foundation.enumeration.listview.base
---

# å…ƒè¯­è¨€ â–¸ å¯æšä¸¾æ€§ â–¸ ç´¯ç§¯åˆ—è¡¨è§†è§’ â–¸ å®šä¹‰

æœ¬ç« ä»‹ç»æšä¸¾çš„ç¬¬2ç§è§†è§’.

```agda
{-# OPTIONS --lossy-unification #-}
module Foundation.Function.Enumeration.ListView.Base where

open import Foundation.Prelude
open import Foundation.Prop.Logic
open import Foundation.Prop.Iff
open import Foundation.Prop.Truncation
open import Foundation.Data.Nat.AlternativeOrder
open import Foundation.Data.Sum
open import Foundation.Data.Sigma
```

æˆ‘ä»¬éœ€è¦åŒæ—¶è°ˆè®ºåˆ—è¡¨çš„ `_âˆˆ_` å’Œå‘é‡çš„ `_âˆˆ_`, åˆ†åˆ«è®°ä½œ `_âˆˆá´¸_` å’Œ `_âˆˆâƒ—_`, ä»¥ç¤ºåŒºåˆ«.

```agda
open import Foundation.Data.List
open import Foundation.Data.List.SetTheoretic
  renaming (_âˆˆ_ to _âˆˆá´¸_)
open import Foundation.Data.Vec
open import Foundation.Data.Vec.SetTheoretic
  renaming (_âˆˆ_ to _âˆˆâƒ—_)
```

## ç´¯ç§¯åˆ—è¡¨

**<u>å®šä¹‰</u>** ç»™å®š `A`, æˆ‘ä»¬æŠŠç”± `A` çš„åˆ—è¡¨ç»„æˆçš„æ— ç©·åºåˆ—è®°ä½œ `ğ•ƒâ‚™ A`, ç®€ç§° `A` çš„åˆ—è¡¨åºåˆ—.

```agda
ğ•ƒâ‚™ : ğ•‹ â„“ â†’ ğ•‹ â„“
ğ•ƒâ‚™ A = â„• â†’ ğ•ƒ A
```

**<u>çº¦å®š</u>** æœ¬ç« ä½¿ç”¨ `f` è¡¨ç¤ºæŸ `A` çš„åˆ—è¡¨åºåˆ—, `m` `n` `o` è¡¨ç¤ºè‡ªç„¶æ•°.

```agda
private variable
  f : ğ•ƒâ‚™ A
  m n o : â„•
```

**<u>å®šä¹‰</u>** åˆ—è¡¨åºåˆ— `f : ğ•ƒâ‚™ A` çš„ä¸€ä¸ªç´¯ç§¯, è®°ä½œ `Cumulation f`, æ˜¯ä¸€ä¸ªä»¥ `n : â„•` ä¸ºç´¢å¼•çš„é›†æ—, å¯¹æ¯ä¸ª `n` éƒ½ç»™å‡ºäº†ä¸€ä¸ª `xs : ğ•ƒ A`, ä½¿å¾— `f (suc n) â‰¡ f n ++ xs` æˆç«‹. å…¶ä¸­ `_++_` æ˜¯åˆ—è¡¨çš„æ‹¼æ¥æ“ä½œ.

```agda
Cumulation : ğ•ƒâ‚™ A â†’ ğ•‹ _
Cumulation f = âˆ€ n â†’ Î£ xs ï¼Œ f (suc n) â‰¡ f n ++ xs
```

ç»™å‡ºäº†ç´¯ç§¯çš„åˆ—è¡¨åºåˆ—ç®€ç§°ç´¯ç§¯åˆ—è¡¨. ç°åœ¨, ç»™å®šä¸€ä¸ªç´¯ç§¯åˆ—è¡¨, ä»¥ä¸‹åˆ—å‡ºå®ƒçš„ä¸€äº›æ€§è´¨. å®ƒä»¬éƒ½éå¸¸æ˜¾ç„¶, æˆ‘ä»¬ç•¥å»è‡ªç„¶è¯­è¨€è¯æ˜.

```agda
module _ (cum : Cumulation f) where
```

**<u>å¼•ç†</u>** ç´¯ç§¯åˆ—è¡¨çš„ä»»æ„ä¸¤ä¸ªé¡¹æœ‰å…±åŒçš„å‰æ®µ, è€Œæ’åœ¨åé¢çš„é¡¹ (ä»¥ä¸‹ç®€ç§°åé¡¹) æ¯”æ’åœ¨å‰é¢çš„é¡¹ (ä»¥ä¸‹ç®€ç§°å‰é¡¹) å¤šäº†ä¸€ä¸ªåæ®µ (å¯èƒ½ä¸ºç©º).

```agda
  cum-â‰¤â†’Î£ : m â‰¤ n â†’ Î£ xs ï¼Œ f n â‰¡ f m ++ xs
  cum-â‰¤â†’Î£ â‰¤-refl = [] , (sym $ ++-identityÊ³ _)
  cum-â‰¤â†’Î£ (â‰¤-step {n} mâ‰¤n) with cum-â‰¤â†’Î£ mâ‰¤n | cum n
  ... | xs , Hx | ys , Hy rewrite Hy | Hx = xs ++ ys , ++-assoc _ _ _
```

**<u>å¼•ç†</u>** ç´¯ç§¯åˆ—è¡¨çš„ä»»æ„ä¸¤ä¸ªé¡¹ä¸­å¿…æœ‰ä¸€ä¸ªé¡¹æ¯”å¦ä¸€ä¸ªé¡¹å¤šä¸€ä¸ªåæ®µ.

```agda
  cum-total : âˆ€ m n â†’ (Î£ xs ï¼Œ f n â‰¡ f m ++ xs) âŠ (Î£ xs ï¼Œ f m â‰¡ f n ++ xs) 
  cum-total m n with â‰¤-total m n
  ... | injâ‚ mâ‰¤n = injâ‚ (cum-â‰¤â†’Î£ mâ‰¤n)
  ... | injâ‚‚ nâ‰¤m = injâ‚‚ (cum-â‰¤â†’Î£ nâ‰¤m)
```

**<u>å¼•ç†</u>** å¯¹ç´¯ç§¯åˆ—è¡¨çš„ä»»æ„ä¸¤ä¸ªé¡¹, å‰é¡¹åŒ…å«äºåé¡¹.

```agda
  cum-â‰¤â†’âŠ† : m â‰¤ n â†’ f m âŠ† f n
  cum-â‰¤â†’âŠ† mâ‰¤n xâˆˆfm with cum-â‰¤â†’Î£ mâ‰¤n
  ... | xs , eq = subst (_ âˆˆá´¸_) eq (âˆˆ-++âºË¡ xâˆˆfm)
```

**<u>å¼•ç†</u>** å¯¹ç´¯ç§¯åˆ—è¡¨çš„ä»»æ„ä¸¤ä¸ªé¡¹, å‰é¡¹çš„é•¿åº¦å°äºç­‰äºåé¡¹çš„é•¿åº¦.

```agda
  cum-length : m â‰¤ n â†’ length (f m) â‰¤ length (f n)
  cum-length {m} {n} mâ‰¤n with cum-â‰¤â†’Î£ mâ‰¤n
  ... | xs , eq = subst (_ â‰¤_) H mâ‰¤m+n where
    H = length (f n)              â‰¡âŸ¨ cong length eq âŸ©
        length (f m ++ xs)        â‰¡âŸ¨ length-++ _ âŸ©
        length (f m) + length xs  âˆ
```

## æšä¸¾çš„å®šä¹‰

**<u>å®šä¹‰</u>** `x : A` åœ¨åˆ—è¡¨åºåˆ— `f : ğ•ƒâ‚™ A` ä¸­çš„è§è¯é›†, è®°ä½œ `Witness f x`, å®šä¹‰ä¸ºæ»¡è¶³ `x âˆˆá´¸ f n` çš„æ‰€æœ‰ `n` (ç§°ä¸º `x` çš„è§è¯) ç»„æˆçš„é›†åˆ.

```agda
Witness : ğ•ƒâ‚™ A â†’ A â†’ ğ•‹ _
Witness f x = Î£ n ï¼Œ x âˆˆá´¸ f n
```

**<u>å®šä¹‰</u>** æˆ‘ä»¬è¯´ `f` è§è¯äº† `x`, è®°ä½œ `f witness x`, å½“ä¸”ä»…å½“è§è¯é›† `Witness f x` æœ‰å€¼, ä¹Ÿå³å­˜åœ¨ `x` çš„è§è¯.

```agda
_witness_ : ğ•ƒâ‚™ A â†’ A â†’ ğ•‹ _
f witness x = âˆ¥ Witness f x âˆ¥â‚
```

**<u>å®šä¹‰</u>** `A` çš„æšä¸¾ `Enum A` æ˜¯ä¸€ä¸ªäºŒå…ƒç»„

1. â€œè§è¯äº†æ‰€æœ‰ `x : A`â€ (è¯¥æ¡ä»¶è®°ä½œ `wit`) çš„åˆ—è¡¨åºåˆ— `enum : ğ•ƒâ‚™ A`
2. `enum` çš„ä¸€ä¸ªç´¯ç§¯ `cum : Cumulation enum`

```agda
record Enum (A : ğ•‹ â„“) : ğ•‹ (â„“ âº) where
  constructor mkEnum
  field
    enum : ğ•ƒâ‚™ A
    cum : Cumulation enum
    wit : âˆ€ x â†’ enum witness x
```

**<u>çº¦å®š</u>** å¯¹ä¸€ç§ç±»å‹, æˆ‘ä»¬åªä¼šè°ˆè®ºå®ƒçš„ä¸€ç§æšä¸¾. å®ƒåœ¨ä¸Šä¸‹æ–‡ä¸­æ˜¯æ˜ç¡®çš„, é¦–æ¬¡å‡ºç°æ—¶ä¼šæ”¾åœ¨æ‹¬å· `â¦ƒ â¦„` ä¸­æˆ–ä½¿ç”¨ `instance` å…³é”®å­—æ¥å£°æ˜, æ‰€ä»¥æ¯æ¬¡æåˆ°æšä¸¾ä¸­çš„æ¦‚å¿µæ—¶ä¸ä¼šä¸€ä¸€å¸¦ä¸Šè¯¥æšä¸¾ä½œä¸ºå‚æ•°, ä»è€Œç²¾ç®€è¡¨è¾¾. è¯¥çº¦å®šè¡¨è¾¾ä¸ºä»¥ä¸‹ä»£ç .

```agda
open Enum â¦ƒ...â¦„ public
```

**<u>å®šä¹‰</u>** æˆ‘ä»¬è¯´ `A` é€’å½’å¯æšä¸¾, å½“ä¸”ä»…å½“å­˜åœ¨ `A` çš„ä¸€ä¸ªæšä¸¾.

```agda
enumerable : ğ•‹ â„“ â†’ ğ•‹ _
enumerable A = âˆ¥ Enum A âˆ¥â‚
```

ä¸å¯é€‰å€¼åºåˆ—è§†è§’ç±»ä¼¼çš„, æˆ‘ä»¬æœ‰æ€§è´¨çš„æšä¸¾. å½“æ€§è´¨ä¸ºæ’çœŸæ—¶, è¿™ä¸¤ç§æšä¸¾å¯ä»¥ç›¸äº’è½¬åŒ–.

```agda
record Enumâ„™ {A : ğ•‹ â„“} (P : A â†’ ğ•‹ â„“â€²) : ğ•‹ (â„“ âŠ” â„“â€²) where
  constructor mkEnumâ„™
  field
    enumâ„™ : ğ•ƒâ‚™ A
    cumâ„™ : Cumulation enumâ„™
    witâ„™ : âˆ€ x â†’ P x â†” enumâ„™ witness x

Enumâ†”â„™ : Enum A â†” Enumâ„™ Î» (_ : A) â†’ âŠ¤
Enumâ†”â„™ = â‡’: (Î» (mkEnum f cum H) â†’ mkEnumâ„™ f cum Î» x â†’ â‡’: (Î» _ â†’ H x) â‡: (Î» _ â†’ tt))
          â‡: (Î» (mkEnumâ„™ f cum H) â†’ mkEnum f cum Î» x â†’ H x .â‡’ tt)

enumerableâ„™ : (A â†’ ğ•‹ â„“) â†’ ğ•‹ _
enumerableâ„™ P = âˆ¥ Enumâ„™ P âˆ¥â‚

enumerableâ†”â„™ : enumerable A â†” enumerableâ„™ Î» (_ : A) â†’ âŠ¤
enumerableâ†”â„™ = â†”-map Enumâ†”â„™
```

**<u>äº‹å®</U>** å¯é€‰å€¼åºåˆ—è§†è§’ä¸ç´¯ç§¯åˆ—è¡¨è§†è§’ç­‰ä»·.  
**<u>è¯æ˜</U>** è§ä»£ç  [`Enumâ†”â“‚`](https://github.com/choukh/MetaLogic/blob/main/src/Foundation/Function/Enumeration/ListView/Properties.agda#L145). âˆ

ç´¯ç§¯åˆ—è¡¨è§†è§’çš„å®šä¹‰å°±åˆ°æ­¤ä¸ºæ­¢äº†, ä½†æˆ‘ä»¬é¢å¤–ä»‹ç»ä¸€ä¸ªç‰¹æ®Šæ„é€ , ç”¨äºåç¯‡è¯æ˜å‘é‡çš„å¯æšä¸¾æ€§.

## åˆ—è¡¨å…ƒç´ çš„ç»„åˆ

**<u>å®šä¹‰</u>** åˆ—è¡¨ `xs : ğ•ƒ A` çš„ `n` ç»´ç»„åˆæ˜¯ä¸€ä¸ªç”± `A` çš„ `n` ç»´å‘é‡ç»„æˆçš„åˆ—è¡¨, ä¾ç»´æ•°çš„ä¸åŒ, é€’å½’å®šä¹‰å¦‚ä¸‹

- å½“ç»´æ•°ä¸ºé›¶æ—¶, è¾“å‡ºç”±ç©ºå‘é‡ç»„æˆçš„å•å…ƒç´ åˆ—è¡¨.
- å½“ç»´æ•°ä¸º `suc n` æ—¶, è¾“å‡ºæ‰€æœ‰å½¢å¦‚ä»¥ä¸‹çš„ `suc n` ç»´å‘é‡æ‰€ç»„æˆçš„åˆ—è¡¨.
  - ä» `xs` ä¸­å–å‡ºä¸€ä¸ªå…ƒç´  `x` ä½œä¸ºå‘é‡çš„å¤´éƒ¨, ä» `xs` çš„ `n` ç»´ç»„åˆä¸­å–å‡ºä¸€ä¸ªå…ƒç´  (`n` ç»´å‘é‡) ä½œä¸ºå‘é‡çš„å°¾éƒ¨, ç»„æˆ `suc n` ç»´å‘é‡.

```agda
combine : ğ•ƒ A â†’ (n : â„•) â†’ ğ•ƒ (ğ• A n)
combine xs zero = [ [] ]
combine xs (suc n) = map (uncurry _âˆ·_) (xs [Ã—] combine xs n)
```

**<u>å¼•ç†</u>** å¯¹ä»»æ„ç´¯ç§¯åˆ—è¡¨å’Œç»´æ•° `n`, å‰é¡¹çš„ `n` ç»´ç»„åˆåŒ…å«äºåé¡¹çš„ `n` ç»´ç»„åˆ.  
**<u>è¯æ˜</u>** ç›´è§‚ä¸Š, ç”±äºå‰é¡¹åŒ…å«äºåé¡¹, å‰é¡¹å…ƒç´ çš„ç»„åˆä¹Ÿå¿…å®šåŒ…å«äºåé¡¹å…ƒç´ çš„ç»„åˆ. âˆ

```agda
combine-â‰¤â†’âŠ† : Cumulation f â†’ m â‰¤ o â†’ combine (f m) n âŠ† combine (f o) n
combine-â‰¤â†’âŠ† {n = zero} _ _ H = H
combine-â‰¤â†’âŠ† {n = suc n} cum mâ‰¤o H with âˆˆmap[Ã—]-elim H
... | x , y , xâˆˆ , yâˆˆ , refl = âˆˆmap[Ã—]-intro (cum-â‰¤â†’âŠ† cum mâ‰¤o xâˆˆ) (combine-â‰¤â†’âŠ† cum mâ‰¤o yâˆˆ)
```

**<u>å¼•ç†</u>** å¯¹ `A` çš„ä»»æ„ç´¯ç§¯åˆ—è¡¨ `f` å’Œä»»æ„ `n` ç»´å‘é‡ `xâƒ—`, å¦‚æœ `f` è§è¯äº† `xâƒ—` çš„æ‰€æœ‰å…ƒç´ , é‚£ä¹ˆç»„åˆçš„åºåˆ— `Î» k â†’ combine (f k) n` è§è¯äº† `xâƒ—`.  
**<u>è¯æ˜</u>** å¯¹ `xâƒ—` å½’çº³. è‹¥ä¸ºç©ºå‘é‡, æ˜¾ç„¶è¢«è§è¯. è‹¥ä¸º `x âˆ· xâƒ—`, å– `x` çš„è§è¯ `m` å’Œ `xâƒ—` çš„è§è¯ `o`, åˆ™ `m + o` å°±æ˜¯ `x âˆ· xâƒ—` çš„è§è¯. âˆ

```agda
combine-wit : Cumulation f â†’ (xâƒ— : ğ• A n) â†’
  (âˆ€ x â†’ x âˆˆâƒ— xâƒ— â†’ f witness x) â†’ (Î» k â†’ combine (f k) n) witness xâƒ—
combine-wit _ [] _ = ex 0 (here refl)
combine-wit {f} cum (x âˆ· xâƒ—) wit = ğŸ™.map2 H (wit x (here refl)) IH where
    IH = combine-wit cum xâƒ— Î» y yâˆˆâƒ— â†’ wit y (there yâˆˆâƒ—)
    H : Witness f x â†’ Witness _ xâƒ— â†’ Witness _ (x âˆ· xâƒ—)
    H (m , Hm) (o , Ho) = m + o , âˆˆmap[Ã—]-intro H1 H2 where
      H1 : x âˆˆá´¸ f (m + o)
      H1 = cum-â‰¤â†’âŠ† cum mâ‰¤m+n Hm
      H2 : xâƒ— âˆˆá´¸ combine (f (m + o)) _
      H2 = combine-â‰¤â†’âŠ† cum mâ‰¤n+m Ho
```

---
> çŸ¥è¯†å…±äº«è®¸å¯åè®®: [CC-BY-NC-SA 4.0](https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh)  
> [GitHub](https://github.com/choukh/MetaLogic/blob/main/src/Foundation/Function/Enumeration/ListView/Base.lagda.md) | [GitHub Pages](https://choukh.github.io/MetaLogic/Foundation.Function.Enumeration.ListView.Base.html) | [è¯­é›€](https://www.yuque.com/ocau/metalogic/foundation.enumeration.listview.base)  
> äº¤æµQç¾¤: 893531731
