---
url: foundation.enumeration.listview.instance
---

# å…ƒè¯­è¨€ â–¸ å¯æšä¸¾æ€§ â–¸ ç´¯ç§¯åˆ—è¡¨è§†è§’ â–¸ å®ä¾‹

æœ¬ç« ç»™å‡ºä¸€äº›åŸºæœ¬ç±»å‹çš„æšä¸¾. æˆ‘ä»¬ä» `Foundation` å’Œæ ‡å‡†åº“ä¸­å¯¼å…¥äº†ç›¸å…³å®šä¹‰å’Œå¼•ç†.

```agda
{-# OPTIONS --lossy-unification #-}
module Foundation.Function.Enumeration.ListView.Instance where
open import Foundation.Function.Enumeration.ListView.Base

open import Foundation.Prelude
open import Foundation.Prop.Logic
open import Foundation.Prop.Truncation

open import Foundation.Data.Nat.AlternativeOrder
open import Foundation.Data.Sigma
open import Foundation.Data.Sum
open import Foundation.Data.List
open import Foundation.Data.List.SetTheoretic
open import Foundation.Data.Vec
```

**<u>çº¦å®š</u>** æœ¬ç« ä½¿ç”¨ `m` `n` `o` è¡¨ç¤ºè‡ªç„¶æ•°.

```agda
private variable
  m n o : â„•
```

**<u>å®ä¾‹/æ„é€ </u>** å¸ƒå°”å€¼æšä¸¾: å–å¸¸åºåˆ— `Î» _ â†’ true âˆ· [ false ]` å³å¯. è¿™æ ·æ¯é¡¹éƒ½æ¯”å‰é¡¹å¤šäº†ç©ºåæ®µ, ä¸”æ˜¾ç„¶æ‰€æœ‰å¸ƒå°”å€¼éƒ½è¢«è§è¯äº†. âˆ

```agda
instance
  enumğ”¹ : Enum ğ”¹
  enumğ”¹ = mkEnum (Î» _ â†’ true âˆ· [ false ]) (Î» n â†’ [] , refl)
    Î» { true â†’  ex 0 $ here refl
      ; false â†’ ex 0 $ there (here refl) }
```

**<u>å®ä¾‹/æ„é€ </u>** è‡ªç„¶æ•°æšä¸¾: åºåˆ—çš„ç¬¬ `0` é¡¹å– `[ 0 ]`, ç¬¬ `suc n` é¡¹å– `e n ++ [ suc n ]` å³å¯. è¿™æ ·æ¯é¡¹éƒ½æ¯”å‰é¡¹å¤šäº†åæ®µ `[ suc n ]`, ä¸”æ˜¾ç„¶æ‰€æœ‰è‡ªç„¶æ•°éƒ½è¢«è§è¯äº†. âˆ

```agda
  enumâ„• : Enum â„•
  enumâ„• = mkEnum e c w where
    e : ğ•ƒâ‚™ â„•
    e zero = [ 0 ]
    e (suc n) = e n ++ [ suc n ]
    c : Cumulation e
    c _ = _ , refl
    w : âˆ€ n â†’ e witness n
    w n = ex n (H n) where
      H : âˆ€ n â†’ n âˆˆ e n
      H zero = here refl
      H (suc n) = âˆˆ++-introÊ³ (here refl)
```

**<u>å®ä¾‹/æ„é€ </u>** å¯æšä¸¾é›†çš„ç¬›å¡å°”ç§¯å¯æšä¸¾: å–ä¸¤é›†åˆæšä¸¾çš„æ¯é¡¹çš„ç¬›å¡å°”ç§¯, ç´¯ç§¯èµ·æ¥å³å¯. `(x , y)` çš„è§è¯æ˜¯ `x` çš„è§è¯åŠ  `y` çš„è§è¯. âˆ

```agda
  enumÃ— : â¦ƒ Enum A â¦„ â†’ â¦ƒ Enum B â¦„ â†’ Enum (A Ã— B)
  enumÃ— {A} {B} = mkEnum e c w where
    e : ğ•ƒâ‚™ (A Ã— B)
    e zero = enum 0 [Ã—] enum 0
    e (suc n) = e n ++ enum n [Ã—] enum n
    c : Cumulation e
    c n = enum n [Ã—] enum n , refl
    w : âˆ€ xy â†’ e witness xy
    w (x , y) = ğŸ™.map2 H (wit x) (wit y) where
      H : Witness enum x â†’ Witness enum y â†’ Witness e (x , y)
      H (m , xâˆˆfm) (n , xâˆˆgn) = suc (m + n) , âˆˆ++-introÊ³ H2 where
        H2 : (x , y) âˆˆ enum (m + n) [Ã—] enum (m + n)
        H2 = âˆˆ[Ã—]-intro (cum-â‰¤â†’âŠ† cum mâ‰¤m+n xâˆˆfm) (cum-â‰¤â†’âŠ† cum mâ‰¤n+m xâˆˆgn)
```

**<u>å®ä¾‹/æ„é€ </u>** å¯æšä¸¾é›†çš„ `n` ç»´å‘é‡å¯æšä¸¾: ç¬¬ `0` é¡¹å–ç©ºåˆ—è¡¨, ç¬¬ `suc m` é¡¹å–å‰ä¸€é¡¹å¹¶ä¸Š `enum m` çš„ `n` ç»´ç»„åˆ. å…¶ä¸­ `enum m` æ˜¯å¯æšä¸¾é›†çš„ç´¯ç§¯åˆ—è¡¨çš„ç¬¬ `m` é¡¹. è§è¯æ¡ä»¶çš„è¯æ˜ç•™ä½œç»ƒä¹ . âˆ

```agda
  enumğ• : â¦ƒ Enum A â¦„ â†’ Enum (ğ• A n)
  enumğ• {A} = mkEnum e c w where
    e : ğ•ƒâ‚™ (ğ• A n)
    e zero = []
    e {n} (suc m) = e m ++ combine (enum m) n

    c : Cumulation e
    c _ = _ , refl

    e-â‰¤â†’âŠ† : {xâƒ— : ğ• A n} â†’ m â‰¤ o â†’ xâƒ— âˆˆ e m â†’ xâƒ— âˆˆ combine (enum o) n
    e-â‰¤â†’âŠ† {m = suc m} smâ‰¤o H with âˆˆ++-elim (e m) H
    ... | injâ‚ xâƒ—âˆˆen   = e-â‰¤â†’âŠ† (m+nâ‰¤oâ‡’nâ‰¤o 1 smâ‰¤o) xâƒ—âˆˆen
    ... | injâ‚‚ xâƒ—âˆˆcomb = combine-â‰¤â†’âŠ† cum (m+nâ‰¤oâ‡’nâ‰¤o 1 smâ‰¤o) xâƒ—âˆˆcomb

    w : (xâƒ— : ğ• A n) â†’ e witness xâƒ—
    w [] = ex 1 (here refl)
    w (x âˆ· xâƒ—) = ğŸ™.map2 H (wit x) (w xâƒ—) where
      H : Witness enum x â†’ Witness e xâƒ— â†’ Witness e (x âˆ· xâƒ—)
      H (m , Hm) (suc n , Hn) = suc m + suc n , âˆˆ++-introÊ³ (âˆˆmap[Ã—]-intro H1 H2) where
        H1 : x âˆˆ enum (m + suc n)
        H1 = cum-â‰¤â†’âŠ† cum mâ‰¤m+n Hm
        H2 : xâƒ— âˆˆ combine (enum (m + suc n)) _
        H2 = e-â‰¤â†’âŠ† mâ‰¤n+m Hn
```

---
> çŸ¥è¯†å…±äº«è®¸å¯åè®®: [CC-BY-NC-SA 4.0](https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh)  
> [GitHub](https://github.com/choukh/MetaLogic/blob/main/src/Foundation/Function/Enumeration/ListView/Instance.lagda.md) | [GitHub Pages](https://choukh.github.io/MetaLogic/Foundation.Function.Enumeration.ListView.Instance.html) | [è¯­é›€](https://www.yuque.com/ocau/metalogic/foundation.enumeration.listview.instance)  
> äº¤æµQç¾¤: 893531731
