---
url: fol.syntax.enumeration
---

# ä¸€é˜¶é€»è¾‘ â–¸ è¯­æ³• â–¸ å…¬å¼çš„æšä¸¾

æœ¬ç¯‡çš„ç›®æ ‡æ˜¯æ„é€ å…¬å¼çš„[æ™®é€šè§†è§’æšä¸¾å‡½æ•°](https://www.yuque.com/ocau/metalogic/foundation.enumeration.plainview) `formulaâ‚™ : â„• â†’ Formula`, æ»¡è¶³å¯¹ä»»æ„ `Ï† : Formula` éƒ½å­˜åœ¨ `n : â„•` ä½¿å¾— `formulaâ‚™ n â‰¡ Ï†`.

```agda
{-# OPTIONS --lossy-unification #-}
open import Foundation.Essential
open import Foundation.Data.Nat.AlternativeOrder
open import Foundation.Data.List.SetTheoretic renaming (_âˆˆ_ to _âˆˆá´¸_)
import Foundation.Function.Enumeration.PlainView as Plain

open import FOL.Language
module FOL.Syntax.Enumeration (â„’ : Language) where
open import FOL.Syntax.Base â„’
open import FOL.Syntax.Discrete â„’
open import FOL.Syntax.FreshVariables â„’
instance _ = â„’

private variable
  m n : â„•
```

## é¡¹çš„æšä¸¾

**<u>å®ä¾‹/æ„é€ </u>** é¡¹çš„æšä¸¾ç”±ä»¥ä¸‹ `e`, `c`, `w` ä¸‰éƒ¨åˆ†æ„æˆ:

```agda
instance
  enumTerm : Enum Term
  enumTerm = mkEnum e c w where
```

### 1. é¡¹çš„åˆ—è¡¨çš„æ— ç©·åºåˆ— `e`

æˆ‘ä»¬éœ€è¦åŒæ—¶äº’é€’å½’æ„é€ æŸå‡½æ•° `f : ğ“•` çš„æ‰€æœ‰ `e n` åº”ç”¨, è®°ä½œ `apps n f`, å®ƒæ˜¯ `f` åº”ç”¨äº `e n` çš„æ‰€æœ‰ `âˆ£ f âˆ£á¶ ` ç»´ç»„åˆæ‰€å¾—åˆ°çš„é‚£äº›é¡¹æ‰€ç»„æˆçš„åˆ—è¡¨.

```agda
    e : ğ•ƒâ‚™ Term
    apps : â„• â†’ ğ“• â†’ ğ•ƒ Term
    apps n f = map (f $Ì‡_) (combine (e n) âˆ£ f âˆ£á¶ )
```

æˆ‘ä»¬é€’å½’å®šä¹‰ `e` å¦‚ä¸‹:
- è¾“å…¥ `zero` æ—¶, è¾“å‡ºç©ºåˆ—è¡¨.
- è¾“å…¥ `suc n` æ—¶, è¾“å‡º `e n` å¹¶ä¸Š `[ # n ]`, å†å¹¶ä¸Šä»¥*ä¸€äº›* `f : ğ“•` ä¸ºä¸‹æ ‡çš„é›†æ— `apps n` çš„å¹¶ (`concat`). å…¶ä¸­*ä¸€äº›* `f : ğ“•` æ˜¯æŒ‡å‡½æ•°ç¬¦å·çš„æšä¸¾å‡½æ•° `enum` (ç”±è¯­è¨€çš„å®šä¹‰, å‡½æ•°ç¬¦å·é›† `ğ“•` å¯æšä¸¾) åº”ç”¨äº `n` æ‰€è¾“å‡ºçš„é‚£äº› `f`.

æ­¤å®šä¹‰ç”¨ä¼ ç»Ÿé›†åˆè®ºç¬¦å·å¯è¡¨è¿°ä¸º

$$
\begin{align*}
e(0) &= \emptyset\\
e(n^+) &= e(n) \cup \{\#n\} \cup \bigcup\{ apps(n, f) \mid f \in enum(n) \}
\end{align*}
$$

```agda
    e zero = []
    e (suc n) = e n ++ # n âˆ· concat (map (apps n) (enum n))
```

### 2. `e` çš„ç´¯ç§¯ `c`

ç”± `e` çš„å®šä¹‰ç«‹å³å¯å¾—å…¶ç´¯ç§¯.

```agda
    c : Cumulation e
    c _ = _ , refl
```

### 3. å‘½é¢˜ `w` : `e` è§è¯äº†ä»»æ„é¡¹ `t`

æˆ‘ä»¬ç”¨é¡¹çš„ç»“æ„å½’çº³æ³•è¯æ˜ `w`:

- è¦è¯ `e` è§è¯äº†ä»»æ„å˜å…ƒ. è§‚å¯Ÿ `e` çš„å®šä¹‰, æ˜¾ç„¶æˆç«‹.
- è¦è¯ `e` è§è¯äº†ä»»æ„å‡½æ•°åº”ç”¨ `f $Ì‡ tâƒ—`, å·²çŸ¥ `tâƒ—` ä¸­çš„é¡¹éƒ½è¢« `e` è§è¯ (å½’çº³å‡è®¾).
  - ç”±è¯­è¨€çš„å®šä¹‰, å‡½æ•°ç¬¦å·é›† `ğ“•` å¯æšä¸¾; ç”±å…ƒè¯­è¨€çš„çŸ¥è¯†, é¡¹çš„ `âˆ£ f âˆ£á¶ ` ç»´å‘é‡å¯æšä¸¾.
    ç”±å½’çº³å‡è®¾å’Œæ¶‰åŠåˆ—è¡¨ç»„åˆçš„å¼•ç† `combine-wit`, åªè¦æ„é€ ä¸€ä¸ªä» `f` çš„è§è¯å’Œ `tâƒ—` çš„è§è¯åˆ° `f $Ì‡ tâƒ—` çš„è§è¯çš„è½¬æ¢å‡½æ•°, å°±è¯æ˜äº† `e` è§è¯ `f $Ì‡ tâƒ—`.
    - åˆ†åˆ«å– `f` å’Œ `tâƒ—` çš„è§è¯ `m` å’Œ `n`. ç”±æšä¸¾å‡½æ•°çš„ç´¯ç§¯æ€§, `m + n` ä¹Ÿæ˜¯ `f` å’Œ `tâƒ—` çš„è§è¯. æ‰€ä»¥ç”± `e` çš„å®šä¹‰, `suc m + n` è§è¯äº† `f $Ì‡ tâƒ—`. âˆ

```agda
    w : âˆ€ t â†’ e witness t
    w = term-elim H# H$Ì‡ where
      H# : âˆ€ n â†’ e witness # n
      H# n = ex (suc n) $ âˆˆ-++âºÊ³ (here refl)
      H$Ì‡ : âˆ€ f tâƒ— â†’ (âˆ€ t â†’ t âˆˆâƒ— tâƒ— â†’ e witness t) â†’ e witness (f $Ì‡ tâƒ—)
      H$Ì‡ f tâƒ— IH = ğŸ™.map2 H (wit f) (combine-wit c tâƒ— IH) where
        H : Witness _ f â†’ Witness _ tâƒ— â†’ Witness _ (f $Ì‡ tâƒ—)
        H (m , Hm) (n , Hn) = suc m + n , âˆˆ-++âºÊ³ (there $ âˆˆ-concatâºâ€² H1 H2) where
          H1 : f $Ì‡ tâƒ— âˆˆá´¸ apps (m + n) f
          H1 = âˆˆmap-intro (combine-â‰¤â†’âŠ† c mâ‰¤n+m Hn) refl
          H2 : apps (m + n) f âˆˆá´¸ map (apps (m + n)) (enum (m + n))
          H2 = âˆˆmap-intro (cum-â‰¤â†’âŠ† cum mâ‰¤m+n Hm) refl
```

## å…¬å¼çš„æšä¸¾

**<u>å®ä¾‹/æ„é€ </u>** å…¬å¼çš„æšä¸¾ç”±ä»¥ä¸‹ `e`, `c`, `w` ä¸‰éƒ¨åˆ†æ„æˆ:

```agda
instance
  enumFormula : Enum Formula
  enumFormula = mkEnum e c w where
```

### 1. å…¬å¼çš„åˆ—è¡¨çš„æ— ç©·åºåˆ— `e`

æˆ‘ä»¬å…ˆå®šä¹‰æŸå…³ç³» `R : ğ“¡` çš„æ‰€æœ‰ `n`-é˜¶åº”ç”¨, è®°ä½œ `apps n R`. å®ƒæ˜¯ `R` åº”ç”¨äºé¡¹çš„*ä¸€äº›* `âˆ£ R âˆ£á´¿` ç»´å‘é‡æ‰€å¾—åˆ°çš„å…¬å¼æ‰€ç»„æˆçš„åˆ—è¡¨. å…¶ä¸­é¡¹çš„*ä¸€äº›* `âˆ£ R âˆ£á´¿` ç»´å‘é‡æ˜¯æŒ‡é¡¹çš„`âˆ£ R âˆ£á´¿` ç»´å‘é‡æšä¸¾å‡½æ•° `enum` (ç”±äºé¡¹å¯æšä¸¾, æ‰€ä»¥é¡¹çš„å›ºå®šç»´å‘é‡ä¹Ÿå¯æšä¸¾) åº”ç”¨äº `n` æ‰€è¾“å‡ºçš„é‚£äº›å‘é‡.

```agda
    apps : â„• â†’ ğ“¡ â†’ ğ•ƒ Formula
    apps n R = map (R $Ì‡_) (enum n)
```

æ¥ç€é€’å½’å®šä¹‰ `e` å¦‚ä¸‹:

- è¾“å…¥ `zero` æ—¶, è¾“å‡º `[ âŠ¥Ì‡ ]`.
- è¾“å…¥ `suc n` æ—¶, è¾“å‡º `e n` å¹¶ä¸Šç”± `e n` ä¸­å…¬å¼äº§ç”Ÿçš„æ‰€æœ‰å…¨ç§°é‡åŒ–å¼å’Œæ‰€æœ‰è•´å«å¼, ä»¥åŠ*ä¸€äº›* `R : ğ“¡` çš„æ‰€æœ‰ `n`-é˜¶åº”ç”¨. å…¶ä¸­*ä¸€äº›* `R : ğ“¡` æ˜¯æŒ‡å…³ç³»ç¬¦å·çš„æšä¸¾å‡½æ•° `enum` (ç”±è¯­è¨€çš„å®šä¹‰, å…³ç³»ç¬¦å·é›† `ğ“¡` å¯æšä¸¾) åº”ç”¨äº `n` æ‰€è¾“å‡ºçš„é‚£äº› `R : ğ“¡`.

```agda
    e : ğ•ƒâ‚™ Formula
    e zero = [ âŠ¥Ì‡ ]
    e (suc n) = e n
      ++ map âˆ€Ì‡_ (e n)
      ++ map (uncurry _â†’Ì‡_) (e n [Ã—] e n)
      ++ concat (map (apps n) (enum n))
```

### 2. `e` çš„ç´¯ç§¯ `c`

ç”± `e` çš„å®šä¹‰ç«‹å³å¯å¾—å…¶ç´¯ç§¯.

```agda
    c : Cumulation e
    c _ = _ , refl
```

### 3. å‘½é¢˜ `w` : `e` è§è¯äº†ä»»æ„å…¬å¼ `Ï†`

ç”± `e` çš„å®šä¹‰, æ˜¾ç„¶, `e` è§è¯äº† `âŠ¥Ì‡`, ä»¥åŠä»»æ„å…¨ç§°é‡åŒ–å¼å’Œè•´å«å¼. è€Œå¯¹äºå…³ç³»åº”ç”¨, ä½¿ç”¨ä¸é¡¹çš„æšä¸¾å‡½æ•°è§è¯æ‰€æœ‰å‡½æ•°åº”ç”¨ç±»ä¼¼çš„æ–¹æ³•å¯è¯. âˆ

```agda
    w : âˆ€ Ï† â†’ e witness Ï†
    w âŠ¥Ì‡ = ex 0 (here refl)
    w (âˆ€Ì‡ Ï†) = ğŸ™.map H (w Ï†) where
      H : Witness e Ï† â†’ Witness e (âˆ€Ì‡ Ï†)
      H (n , Hn) = suc n , (âˆˆ-++âºÊ³ $ âˆˆ-++âºË¡ $ âˆˆmap-intro Hn refl)
    w (Ï† â†’Ì‡ Ïˆ) = ğŸ™.map2 H (w Ï†) (w Ïˆ) where
      H : Witness e Ï† â†’ Witness e Ïˆ â†’ Witness e (Ï† â†’Ì‡ Ïˆ)
      H (m , Hm) (n , Hn) = suc m + n , (âˆˆ-++âºÊ³ $ âˆˆ-++âºÊ³ $ âˆˆ-++âºË¡ $ âˆˆmap[Ã—]-intro
        (cum-â‰¤â†’âŠ† c mâ‰¤m+n Hm) (cum-â‰¤â†’âŠ† c mâ‰¤n+m Hn))
    w (R $Ì‡ tâƒ—) = ğŸ™.map2 H (wit R) (wit tâƒ—) where
      H : Witness enum R â†’ Witness enum tâƒ— â†’ Witness e (R $Ì‡ tâƒ—)
      H (m , Hm) (n , Hn) = suc m + n , (âˆˆ-++âºÊ³ $ âˆˆ-++âºÊ³ $ âˆˆ-++âºÊ³ $ âˆˆ-concatâºâ€² H1 H2) where
          H1 : R $Ì‡ tâƒ— âˆˆá´¸ apps (m + n) R
          H1 = âˆˆmap-intro (cum-â‰¤â†’âŠ† cum mâ‰¤n+m Hn) refl
          H2 : apps (m + n) R âˆˆá´¸ map (apps (m + n)) (enum (m + n))
          H2 = âˆˆmap-intro (cum-â‰¤â†’âŠ† cum mâ‰¤m+n Hm) refl
```

**<u>å¼•ç†</u>** å…¬å¼çš„ç´¯ç§¯åˆ—è¡¨æ˜¯çœŸç´¯ç§¯.  
**<u>è¯æ˜</u>** è§‚å¯Ÿå®šä¹‰ä¸éš¾çœ‹å‡ºåˆ—è¡¨çš„é•¿åº¦æ˜¯ä¸¥æ ¼é€’å¢çš„. âˆ

```agda
enumFormula-proper : âˆ€ n â†’ length (enum â¦ƒ enumFormula â¦„ n) > n
enumFormula-proper zero = â‰¤-refl
enumFormula-proper (suc n) = subst (_> _) (length-++-++ _ _) (<-â‰¤-trans H mâ‰¤m+n) where
  H : length (enum n) + length (map âˆ€Ì‡_ _) > 1 + n
  H = +-mono-â‰¤-< (cum-length cum zâ‰¤n) (subst (_ <_) (length-map _ _) (enumFormula-proper n))
```

**<u>å®šç†</u>** å­˜åœ¨å…¬å¼çš„æšä¸¾å‡½æ•° `formulaâ‚™ : â„• â†’ Formula`, æ»¡è¶³å¯¹ä»»æ„ `Ï† : Formula` éƒ½å­˜åœ¨ `n : â„•` ä½¿å¾— `formulaâ‚™ n â‰¡ Ï†`.  
**<u>è¯æ˜</u>** ç”±äºå…¬å¼ç±»å‹ `Formula` æ˜¯ç¦»æ•£é›†ä¸”å¯æšä¸¾, ä¸”å…¶ä¸­çš„ç´¯ç§¯åˆ—è¡¨æ˜¯çœŸç´¯ç§¯, ç¬¦åˆæ™®é€šè§†è§’æšä¸¾å‡½æ•° `Plain.enum` çš„è¦æ±‚, æŒ‰å…¶å®šä¹‰æ„é€ å³å¾—ç¬¦åˆè¦æ±‚çš„ `formulaâ‚™ : â„• â†’ Formula`. âˆ

```agda
formulaâ‚™ : â„• â†’ Formula
formulaâ‚™ = Plain.enum enumFormula-proper

formulaâ‚™-wit : âˆ€ Ï† â†’ âˆƒ n ï¼Œ formulaâ‚™ n â‰¡ Ï†
formulaâ‚™-wit = Plain.wit enumFormula-proper
```

## æ–°å˜å…ƒçš„æšä¸¾æ€§è´¨

```agda
termEnum-fresh : m â‰¤ n â†’ t âˆˆá´¸ enum m â†’ freshâ‚œ n t
termEnum-fresh {(zero)} _ ()
termEnum-fresh {suc m} le tâˆˆ with âˆˆ-++â» (enum m) tâˆˆ
... | injâ‚ tâˆˆ = termEnum-fresh (m+nâ‰¤oâ‡’nâ‰¤o 1 le) tâˆˆ
... | injâ‚‚ (here refl) = fresh# Î» { refl â†’ 1+nâ‰°n le }
termEnum-fresh {t = # o} _ _ | injâ‚‚ (there tâˆˆ) with âˆˆ-concatâ»â€² _ tâˆˆ
... | _ , tâˆˆts , tsâˆˆ with âˆˆmap-elim tsâˆˆ
... | _ , _ , refl with âˆˆmap-elim tâˆˆts
... | _ , _ , ()
termEnum-fresh {t = f $Ì‡ tâƒ—} le _ | injâ‚‚ (there tâˆˆ) with âˆˆ-concatâ»â€² _ tâˆˆ
... | _ , tâˆˆts , tsâˆˆ with âˆˆmap-elim tsâˆˆ
... | _ , _ , refl with âˆˆmap-elim tâˆˆts
... | _ , tâƒ—âˆˆ , refl with âˆˆcombine-elim tâƒ—âˆˆ
... | H = fresh$Ì‡ Î» t tâˆˆtâƒ— â†’ termEnum-fresh (m+nâ‰¤oâ‡’nâ‰¤o 1 le) (H tâˆˆtâƒ—)
```

```agda
--formulaâ‚™-fresh : m â‰¤ n â†’ fresh n (formulaâ‚™ m)
--formulaâ‚™-fresh le = {!   !}
```

---
> çŸ¥è¯†å…±äº«è®¸å¯åè®®: [CC-BY-NC-SA 4.0](https://creativecommons.org/licenses/by-nc-sa/4.0/deed.zh)  
> [GitHub](https://github.com/choukh/MetaLogic/blob/main/src/FOL/Syntax/Enumeration.lagda.md) | [GitHub Pages](https://choukh.github.io/MetaLogic/FOL.Syntax.Enumeration.html) | [è¯­é›€](https://www.yuque.com/ocau/metalogic/fol.syntax.enumeration)  
> äº¤æµQç¾¤: 893531731
